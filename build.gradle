buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.36.0'
    }
}

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

def projectName = 'tweenmachine'

group = 'com.github.tommyettinger'

version = "$VERSION_NAME" // You can set the version in gradle.properties .

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

// This just makes sure that a Javadoc JAR and a sources JAR are produced.
mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

// This sets the Javadoc JAR to have hopefully the correct name. This may need adjustment.
plainJavadocJar.archiveBaseName.set(projectName)

// There's pretty much no reason to use any encoding other than UTF-8 .
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

// Disable JDK 8's doclint
// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc).tap {
            configureEach {
                // The -quiet is because of some sort of weird JDK JavaCompiler bug:
                // https://discuss.gradle.org/t/passing-arguments-to-compiler-and-javadoc/1661
                options.addStringOption('Xdoclint:none,-missing', '-quiet')
            }
        }
    }
}

compileJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

// JavaDocs will be published inside the docs/ folder, which you can easily put on GitHub Pages
// in your repo settings.
// You may instead want to remove this line if frequent doc changes use up too much repo space,
// or if you use a different version control host that doesn't host HTML like this.
javadoc {
    destinationDir = file('docs/apidocs')
    options {
        links('https://javadoc.io/doc/com.badlogicgames.gdx/gdx/latest/')
    }
}

apply plugin: 'idea'
// This makes IDEA avoid including generated JavaDocs (which are HTML files) in any search results.
// If you changed where JavaDocs are published above, you should change the next line, too.
idea.module.excludeDirs += [file("docs/")]

jar {
    archiveBaseName.set(projectName)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
//    maven { url = 'https://central.sonatype.com/repository/maven-snapshots/' }
    // JitPack is a good repo to have if you depend on commit versions.
    // Having the below repo might cause problems if uploading to Maven Central.
    // JitPack commit dependencies are much better than "-SNAPSHOT" version dependencies!
    maven { url = 'https://jitpack.io' }
}

dependencies {
// Using libGDX 1.13.1 is preferred over requiring 1.13.5 because 1.13.5 has serious issues on Android.
    //noinspection GDXOutdatedVersionGradleGroovy
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "space.earlygrey:shapedrawer:2.6.0"
    testImplementation "junit:junit:4.13.2"
}
